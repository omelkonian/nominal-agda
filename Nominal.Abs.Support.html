<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Nominal.Abs.Support</title><link rel='stylesheet' href='https://cdnjs.cloudflare.com/ajax/libs/github-fork-ribbon-css/0.2.3/gh-fork-ribbon.min.css'/><style>.github-fork-ribbon:before { background-color: #333; }</style><link rel="stylesheet" href="css/Agda.css"></head><body><a class='github-fork-ribbon'href='https://github.com/omelkonian/nominal-agda/tree/master/Nominal/Abs/Support.agda'data-ribbon='Source code on Github' title='Source code on Github'>Source code on Github</a><pre class="Agda"><a id="1" class="Keyword">open</a> <a id="6" class="Keyword">import</a> <a id="13" href="Prelude.Init.html" class="Module">Prelude.Init</a><a id="25" class="Symbol">;</a> <a id="27" class="Keyword">open</a> <a id="32" href="Prelude.Init.html#397" class="Module">SetAsType</a>
<a id="42" class="Keyword">open</a> <a id="47" href="Prelude.Init.html#6704" class="Module">L.Mem</a>
<a id="53" class="Keyword">open</a> <a id="58" class="Keyword">import</a> <a id="65" href="Prelude.DecEq.html" class="Module">Prelude.DecEq</a>
<a id="79" class="Keyword">open</a> <a id="84" class="Keyword">import</a> <a id="91" href="Prelude.Setoid.html" class="Module">Prelude.Setoid</a>
<a id="106" class="Keyword">open</a> <a id="111" class="Keyword">import</a> <a id="118" href="Prelude.Bifunctor.html" class="Module">Prelude.Bifunctor</a>
<a id="136" class="Keyword">open</a> <a id="141" class="Keyword">import</a> <a id="148" href="Prelude.InferenceRules.html" class="Module">Prelude.InferenceRules</a>
<a id="171" class="Keyword">open</a> <a id="176" class="Keyword">import</a> <a id="183" href="Prelude.InfEnumerable.html" class="Module">Prelude.InfEnumerable</a>

<a id="206" class="Keyword">module</a> <a id="213" href="Nominal.Abs.Support.html" class="Module">Nominal.Abs.Support</a> <a id="233" class="Symbol">(</a><a id="234" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a> <a id="239" class="Symbol">:</a> <a id="241" href="Agda.Primitive.html#326" class="Primitive">Type</a><a id="245" class="Symbol">)</a> <a id="247" class="Symbol">⦃</a> <a id="249" href="Nominal.Abs.Support.html#249" class="Bound">_</a> <a id="251" class="Symbol">:</a> <a id="253" href="Prelude.DecEq.Core.html#114" class="Record">DecEq</a> <a id="259" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a> <a id="264" class="Symbol">⦄</a> <a id="266" class="Symbol">⦃</a> <a id="268" href="Nominal.Abs.Support.html#268" class="Bound">_</a> <a id="270" class="Symbol">:</a> <a id="272" href="Prelude.InfEnumerable.html#531" class="Record">Enumerable∞</a> <a id="284" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a> <a id="289" class="Symbol">⦄</a> <a id="291" class="Keyword">where</a>

<a id="298" class="Keyword">open</a> <a id="303" class="Keyword">import</a> <a id="310" href="Nominal.New.html" class="Module">Nominal.New</a>      <a id="327" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a>
<a id="332" class="Keyword">open</a> <a id="337" class="Keyword">import</a> <a id="344" href="Nominal.Swap.html" class="Module">Nominal.Swap</a>     <a id="361" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a>
<a id="366" class="Keyword">open</a> <a id="371" class="Keyword">import</a> <a id="378" href="Nominal.Support.html" class="Module">Nominal.Support</a>  <a id="395" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a>
<a id="400" class="Keyword">open</a> <a id="405" class="Keyword">import</a> <a id="412" href="Nominal.Abs.Base.html" class="Module">Nominal.Abs.Base</a> <a id="429" href="Nominal.Abs.Support.html#234" class="Bound">Atom</a>

<a id="435" class="Keyword">module</a> <a id="442" href="Nominal.Abs.Support.html#442" class="Module">_</a> <a id="444" class="Symbol">{</a><a id="445" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="447" class="Symbol">:</a> <a id="449" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="454" href="Prelude.Init.html#238" class="Generalizable">ℓ</a><a id="455" class="Symbol">}</a>
  <a id="459" class="Symbol">⦃</a> <a id="461" href="Nominal.Abs.Support.html#461" class="Bound">_</a> <a id="463" class="Symbol">:</a> <a id="465" href="Prelude.Setoid.html#109" class="Record">ISetoid</a> <a id="473" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="475" class="Symbol">⦄</a> <a id="477" class="Symbol">⦃</a> <a id="479" href="Nominal.Abs.Support.html#479" class="Bound">_</a> <a id="481" class="Symbol">:</a> <a id="483" href="Prelude.Setoid.html#519" class="Record">Setoid-Laws</a> <a id="495" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="497" class="Symbol">⦄</a>
  <a id="501" class="Symbol">⦃</a> <a id="503" href="Nominal.Abs.Support.html#503" class="Bound">_</a> <a id="505" class="Symbol">:</a> <a id="507" href="Nominal.Swap.Base.html#390" class="Record">Swap</a> <a id="512" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="514" class="Symbol">⦄</a> <a id="516" class="Symbol">⦃</a> <a id="518" href="Nominal.Abs.Support.html#518" class="Bound">_</a> <a id="520" class="Symbol">:</a> <a id="522" href="Nominal.Swap.Base.html#2635" class="Record">SwapLaws</a> <a id="531" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="533" class="Symbol">⦄</a>
  <a id="537" class="Symbol">⦃</a> <a id="539" href="Nominal.Abs.Support.html#539" class="Bound">_</a> <a id="541" class="Symbol">:</a> <a id="543" href="Nominal.Support.html#1145" class="Record">FinitelySupported</a> <a id="561" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="563" class="Symbol">⦄</a> <a id="565" class="Keyword">where</a>

  <a id="574" class="Comment">-- abstractions over finitely supported types are themselves finite</a>
  <a id="644" class="Keyword">instance</a>
    <a id="657" href="Nominal.Abs.Support.html#657" class="Function">FinSupp-Abs</a> <a id="669" class="Symbol">:</a> <a id="671" href="Nominal.Support.html#1145" class="Record">FinitelySupported</a> <a id="689" class="Symbol">(</a><a id="690" href="Nominal.Abs.Base.html#400" class="Record">Abs</a> <a id="694" href="Nominal.Abs.Support.html#445" class="Bound">A</a><a id="695" class="Symbol">)</a>
    <a id="701" href="Nominal.Abs.Support.html#657" class="Function">FinSupp-Abs</a> <a id="713" class="Symbol">.</a><a id="714" href="Nominal.Support.html#1279" class="Field">∀fin</a> <a id="719" class="Symbol">(</a><a id="720" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="724" href="Nominal.Abs.Support.html#724" class="Bound">x</a> <a id="726" href="Nominal.Abs.Support.html#726" class="Bound">t</a><a id="727" class="Symbol">)</a> <a id="729" class="Symbol">=</a>
      <a id="737" class="Keyword">let</a> <a id="741" href="Nominal.Abs.Support.html#741" class="Bound">xs</a> <a id="744" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="746" href="Nominal.Abs.Support.html#746" class="Bound">p</a> <a id="748" class="Symbol">=</a> <a id="750" href="Nominal.Support.html#1279" class="Field">∀fin</a> <a id="755" href="Nominal.Abs.Support.html#726" class="Bound">t</a>
      <a id="763" class="Keyword">in</a> <a id="766" href="Nominal.Abs.Support.html#724" class="Bound">x</a> <a id="768" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="770" href="Nominal.Abs.Support.html#741" class="Bound">xs</a> <a id="773" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="775" class="Symbol">λ</a> <a id="777" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="779" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="781" href="Nominal.Abs.Support.html#781" class="Bound">y∉</a> <a id="784" href="Nominal.Abs.Support.html#784" class="Bound">z∉</a> <a id="787" class="Symbol">→</a>
      <a id="795" href="Relation.Binary.Reasoning.Base.Single.html#1916" class="Function Operator">begin</a>
        <a id="809" href="Nominal.Swap.Base.html#609" class="Function Operator">⦅</a> <a id="811" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="813" href="Nominal.Swap.Base.html#609" class="Function Operator">↔</a> <a id="815" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="817" href="Nominal.Swap.Base.html#609" class="Function Operator">⦆</a> <a id="819" class="Symbol">(</a><a id="820" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="824" href="Nominal.Abs.Support.html#724" class="Bound">x</a> <a id="826" href="Nominal.Abs.Support.html#726" class="Bound">t</a><a id="827" class="Symbol">)</a>
      <a id="835" href="Relation.Binary.Reasoning.Base.Single.html#2470" class="Function Operator">≡⟨⟩</a>
        <a id="847" class="Comment">-- ⦅ 𝕒 ↔ 𝕓 ⦆ (f 𝕔) ≈ (⦅ 𝕒 ↔ 𝕓 ⦆ f) (⦅ 𝕒 ↔ 𝕓 ⦆ 𝕔)</a>
        <a id="904" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="908" class="Symbol">(</a><a id="909" href="Nominal.Swap.Base.html#609" class="Function Operator">⦅</a> <a id="911" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="913" href="Nominal.Swap.Base.html#609" class="Function Operator">↔</a> <a id="915" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="917" href="Nominal.Swap.Base.html#609" class="Function Operator">⦆</a> <a id="919" href="Nominal.Abs.Support.html#724" class="Bound">x</a><a id="920" class="Symbol">)</a> <a id="922" class="Symbol">(</a><a id="923" href="Nominal.Swap.Base.html#609" class="Function Operator">⦅</a> <a id="925" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="927" href="Nominal.Swap.Base.html#609" class="Function Operator">↔</a> <a id="929" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="931" href="Nominal.Swap.Base.html#609" class="Function Operator">⦆</a> <a id="933" href="Nominal.Abs.Support.html#726" class="Bound">t</a><a id="934" class="Symbol">)</a>
      <a id="942" href="Relation.Binary.Reasoning.Base.Single.html#2182" class="Function">≡⟨</a> <a id="945" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="950" class="Symbol">(λ</a> <a id="953" href="Nominal.Abs.Support.html#953" class="Bound">◆</a> <a id="955" class="Symbol">→</a> <a id="957" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="961" href="Nominal.Abs.Support.html#953" class="Bound">◆</a> <a id="963" class="Symbol">(</a><a id="964" href="Nominal.Swap.Base.html#609" class="Function Operator">⦅</a> <a id="966" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="968" href="Nominal.Swap.Base.html#609" class="Function Operator">↔</a> <a id="970" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="972" href="Nominal.Swap.Base.html#609" class="Function Operator">⦆</a> <a id="974" href="Nominal.Abs.Support.html#726" class="Bound">t</a><a id="975" class="Symbol">))</a>
            <a id="990" href="Function.Base.html#1919" class="Function Operator">$</a> <a id="992" href="Nominal.Swap.Base.html#2228" class="Function">swap-noop</a> <a id="1002" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="1004" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="1006" href="Nominal.Abs.Support.html#724" class="Bound">x</a> <a id="1008" class="Symbol">(λ</a> <a id="1011" class="Keyword">where</a> <a id="1017" href="Nominal.Swap.Base.html#2460" class="InductiveConstructor">♯0</a> <a id="1020" class="Symbol">→</a> <a id="1022" href="Nominal.Abs.Support.html#784" class="Bound">z∉</a> <a id="1025" href="Nominal.Swap.Base.html#2460" class="InductiveConstructor">♯0</a><a id="1027" class="Symbol">;</a> <a id="1029" href="Nominal.Swap.Base.html#2483" class="InductiveConstructor">♯1</a> <a id="1032" class="Symbol">→</a> <a id="1034" href="Nominal.Abs.Support.html#781" class="Bound">y∉</a> <a id="1037" href="Nominal.Swap.Base.html#2460" class="InductiveConstructor">♯0</a><a id="1039" class="Symbol">)</a> <a id="1041" href="Relation.Binary.Reasoning.Base.Single.html#2182" class="Function">⟩</a>
        <a id="1051" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="1055" href="Nominal.Abs.Support.html#724" class="Bound">x</a> <a id="1057" class="Symbol">(</a><a id="1058" href="Nominal.Swap.Base.html#609" class="Function Operator">⦅</a> <a id="1060" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="1062" href="Nominal.Swap.Base.html#609" class="Function Operator">↔</a> <a id="1064" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="1066" href="Nominal.Swap.Base.html#609" class="Function Operator">⦆</a> <a id="1068" href="Nominal.Abs.Support.html#726" class="Bound">t</a><a id="1069" class="Symbol">)</a>
      <a id="1077" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="1080" href="Nominal.Abs.Base.html#1939" class="Function">cong-abs</a> <a id="1089" href="Function.Base.html#1919" class="Function Operator">$</a> <a id="1091" href="Nominal.Abs.Support.html#746" class="Bound">p</a> <a id="1093" href="Nominal.Abs.Support.html#777" class="Bound">y</a> <a id="1095" href="Nominal.Abs.Support.html#779" class="Bound">z</a> <a id="1097" class="Symbol">(</a><a id="1098" href="Nominal.Abs.Support.html#781" class="Bound">y∉</a> <a id="1101" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="1103" href="Data.List.Relation.Unary.Any.html#1272" class="InductiveConstructor">there</a><a id="1108" class="Symbol">)</a> <a id="1110" class="Symbol">(</a><a id="1111" href="Nominal.Abs.Support.html#784" class="Bound">z∉</a> <a id="1114" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="1116" href="Data.List.Relation.Unary.Any.html#1272" class="InductiveConstructor">there</a><a id="1121" class="Symbol">)</a> <a id="1123" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
        <a id="1133" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="1137" href="Nominal.Abs.Support.html#724" class="Bound">x</a> <a id="1139" href="Nominal.Abs.Support.html#726" class="Bound">t</a>
      <a id="1147" href="Relation.Binary.Reasoning.Base.Single.html#2555" class="Function Operator">∎</a> <a id="1149" class="Keyword">where</a> <a id="1155" class="Keyword">open</a> <a id="1160" href="Prelude.Setoid.html#918" class="Module">≈-Reasoning</a>

  <a id="1175" class="Comment">-- Two ways to fix functoriality:</a>
    <a id="1213" class="Comment">-- 1. require that (f : A → A) is equivariant</a>
  <a id="1261" class="Comment">--   2. ...or that it at least has finite support</a>
  <a id="1313" href="Nominal.Abs.Support.html#1313" class="Function">mapAbs</a> <a id="1320" class="Symbol">:</a> <a id="1322" href="Algebra.Core.html#475" class="Function">Op₁</a> <a id="1326" href="Nominal.Abs.Support.html#445" class="Bound">A</a> <a id="1328" class="Symbol">→</a> <a id="1330" href="Algebra.Core.html#475" class="Function">Op₁</a> <a id="1334" class="Symbol">(</a><a id="1335" href="Nominal.Abs.Base.html#400" class="Record">Abs</a> <a id="1339" href="Nominal.Abs.Support.html#445" class="Bound">A</a><a id="1340" class="Symbol">)</a>
      <a id="1348" class="Comment">-- ≈ (A → A) → (Abs A → Abs A)</a>
  <a id="1381" class="Comment">-- T0D0: In order to resolve termination issues (via well-founded recursion),</a>
  <a id="1461" class="Comment">-- we need a more restrainted version of mapAbs with type:</a>
  <a id="1522" class="Comment">-- mapAbs : (x&#39; : Abs A) → (f : (a : A) → a ≺ f → A) → Abs A</a>
  <a id="1585" class="Comment">-- NB: a generalisation would be to say that the size behaviour of</a>
  <a id="1654" class="Comment">--     `mapAbs f` corresponds to that of `f`</a>
  <a id="1701" href="Nominal.Abs.Support.html#1313" class="Function">mapAbs</a> <a id="1708" href="Nominal.Abs.Support.html#1708" class="Bound">f</a> <a id="1710" href="Nominal.Abs.Support.html#1710" class="Bound">x&#39;</a> <a id="1713" class="Symbol">=</a>
    <a id="1719" class="Keyword">let</a> <a id="1723" href="Nominal.Abs.Support.html#1723" class="Bound">a</a> <a id="1725" class="Symbol">=</a> <a id="1727" href="Nominal.Support.html#1889" class="Function">fresh-var</a> <a id="1737" href="Nominal.Abs.Support.html#1710" class="Bound">x&#39;</a> <a id="1740" class="Comment">-- T0D0: ++ supp?? f</a>
    <a id="1765" class="Keyword">in</a> <a id="1768" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="1772" href="Nominal.Abs.Support.html#1723" class="Bound">a</a> <a id="1774" class="Symbol">(</a><a id="1775" href="Nominal.Abs.Support.html#1708" class="Bound">f</a> <a id="1777" href="Function.Base.html#1919" class="Function Operator">$</a> <a id="1779" href="Nominal.Abs.Base.html#550" class="Function">conc</a> <a id="1784" href="Nominal.Abs.Support.html#1710" class="Bound">x&#39;</a> <a id="1787" href="Nominal.Abs.Support.html#1723" class="Bound">a</a><a id="1788" class="Symbol">)</a>

  <a id="1793" href="Nominal.Abs.Support.html#1793" class="Function">freshen</a> <a id="1801" class="Symbol">:</a> <a id="1803" href="Algebra.Core.html#475" class="Function">Op₁</a> <a id="1807" class="Symbol">(</a><a id="1808" href="Nominal.Abs.Base.html#400" class="Record">Abs</a> <a id="1812" href="Nominal.Abs.Support.html#445" class="Bound">A</a><a id="1813" class="Symbol">)</a>
  <a id="1817" href="Nominal.Abs.Support.html#1793" class="Function">freshen</a> <a id="1825" href="Nominal.Abs.Support.html#1825" class="Bound">f</a><a id="1826" class="Symbol">@(</a><a id="1828" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="1832" href="Nominal.Abs.Support.html#1832" class="Bound">a</a> <a id="1834" href="Nominal.Abs.Support.html#1834" class="Bound">t</a><a id="1835" class="Symbol">)</a> <a id="1837" class="Symbol">=</a>
    <a id="1843" class="Keyword">let</a> <a id="1847" href="Nominal.Abs.Support.html#1847" class="Bound">xs</a> <a id="1850" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1852" class="Symbol">_</a> <a id="1854" class="Symbol">=</a> <a id="1856" href="Nominal.Support.html#1279" class="Field">∀fin</a> <a id="1861" href="Nominal.Abs.Support.html#1825" class="Bound">f</a>
        <a id="1871" href="Nominal.Abs.Support.html#1871" class="Bound">b</a> <a id="1873" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1875" href="Nominal.Abs.Support.html#1875" class="Bound">b∉</a> <a id="1878" class="Symbol">=</a> <a id="1880" href="Prelude.InfEnumerable.html#1791" class="Function">minFresh</a> <a id="1889" href="Nominal.Abs.Support.html#1847" class="Bound">xs</a>
    <a id="1896" class="Keyword">in</a> <a id="1899" href="Nominal.Abs.Base.html#446" class="InductiveConstructor">abs</a> <a id="1903" href="Nominal.Abs.Support.html#1871" class="Bound">b</a> <a id="1905" class="Symbol">(</a><a id="1906" href="Nominal.Abs.Base.html#550" class="Function">conc</a> <a id="1911" href="Nominal.Abs.Support.html#1825" class="Bound">f</a> <a id="1913" href="Nominal.Abs.Support.html#1871" class="Bound">b</a><a id="1914" class="Symbol">)</a>
</pre></body></html>